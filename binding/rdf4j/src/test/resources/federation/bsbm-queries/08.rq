# BSBM-08: give me recent reviews in English for a specific product
# PREFIX bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>
# PREFIX foaf: <http://xmlns.com/foaf/0.1/>
# PREFIX rev: <http://purl.org/stuff/rev#>
# PREFIX dc: <http://purl.org/dc/elements/1.1/>
# BASE <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/>
#
# SELECT
#     ?title ?text ?reviewDate ?reviewer ?reviewerName
#     ?rating1 ?rating2 ?rating3 ?rating4
# {
#     ?review bsbm:reviewFor <dataFromProducer/Product${product:percent}> .
#     ?review dc:title ?title .
#     ?review rev:text ?text .
#     ?review bsbm:language "en" .  # in BSBM: langMatches(lang(?text), "en")
#     ?review bsbm:reviewDate ?reviewDate .
#     ?review rev:reviewer ?reviewer .
#     ?reviewer foaf:name ?reviewerName .
#     OPTIONAL { ?review bsbm:rating1 ?rating1 }
#     OPTIONAL { ?review bsbm:rating2 ?rating2 }
#     OPTIONAL { ?review bsbm:rating3 ?rating3 }
#     OPTIONAL { ?review bsbm:rating4 ?rating4 }
# }
# ORDER BY DESC(?reviewDate)
# LIMIT 20
#
# instantiate sample variables
PREFIX bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>
PREFIX rev: <http://purl.org/stuff/rev#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>
BASE <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/>

SELECT
    ?title ?text ?reviewDate ?reviewer ?reviewerName
    ?rating1 ?rating2 ?rating3 ?rating4
{
    ?review bsbm:reviewFor <dataFromProducer/Product23> .  # product = 23
    ?review dc:title ?title .
    ?review rev:text ?text .
    ?review bsbm:language "en" .  # in BSBM: langMatches(lang(?text), "en")
    ?review bsbm:reviewDate ?reviewDate .
    ?review rev:reviewer ?reviewer .
    ?reviewer foaf:name ?reviewerName .
    OPTIONAL { ?review bsbm:rating1 ?rating1 }
    OPTIONAL { ?review bsbm:rating2 ?rating2 }
    OPTIONAL { ?review bsbm:rating3 ?rating3 }
    OPTIONAL { ?review bsbm:rating4 ?rating4 }
}
ORDER BY DESC(?reviewDate)
LIMIT 20