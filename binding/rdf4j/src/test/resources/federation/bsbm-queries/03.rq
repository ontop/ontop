# BSBM-03: find products having some specific features and not having one feature
# PREFIX bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>
# PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
# BASE <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/>
#
# SELECT ?product ?label
# {
#     ?product rdfs:label ?label .
#     ?product a bsbm:Product .
#     ?product bsbm:type <ProductType${type:percent}> .  # in BSBM: ?product a %ProductType%
#     ?product bsbm:productFeature <ProductFeature${feature2:none}> .
#     ?product bsbm:productPropertyNumeric1 ?p1 .
#     FILTER (?p1 > ${propertynum1_lo:none})
#     ?product bsbm:productPropertyNumeric3 ?p3 .
#     FILTER (?p3 < ${propertynum3_hi:none})
#     OPTIONAL {
#         ?product bsbm:productFeature <ProductFeature${feature_missing:none}> .
#         ?product rdfs:label ?testVar
#     }
#     FILTER (!bound(?testVar))
# }
# ORDER BY ?label
# LIMIT 10
#
# instantiate sample variables
PREFIX bsbm: <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/vocabulary/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
BASE <http://www4.wiwiss.fu-berlin.de/bizer/bsbm/v01/instances/>

SELECT ?product ?label
{
    ?product rdfs:label ?label .
    ?product a bsbm:Product .
    ?product bsbm:type <ProductType4> .  # type = 4 / in BSBM: ?product a %ProductType%
    ?product bsbm:productFeature <ProductFeature87> .  # feature2 = 87
    ?product bsbm:productPropertyNumeric1 ?p1 .
    FILTER (?p1 > 21)  # propertynum1_lo = 21
    ?product bsbm:productPropertyNumeric3 ?p3 .
    FILTER (?p3 < 1246)  # propertynum3_hi = 1246
    OPTIONAL {
        ?product bsbm:productFeature <ProductFeature611> .  # feature_missing = 611
        ?product rdfs:label ?testVar
    }
    FILTER (!bound(?testVar))
}
ORDER BY ?label
LIMIT 10